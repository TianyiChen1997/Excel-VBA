Sub Rotor_Buy()
'
'   This is for weekly restock buy quantity calculation.
'   Before you run this sub, you need to run the previous "Rotor_Cleanup" sub in the "Weekly_Update_Cleanup" module to clean up the sheets first,
'   and then copy & paste the purchased parts manually onto cell CK3 in the main sheet.
'

    Application.ScreenUpdating = False

'   You may want to change your worksheets names here
    Dim summary_ws, buy_ws As Worksheet
    Dim last_row As Long
    Set summary_ws = Worksheets("ROTOR SUMMARY")
    Set buy_ws = Worksheets("Buy")
    last_row = summary_ws.Range("A" & Rows.Count).End(xlUp).Row
    
'   Assume we have purchasd parts copied & pasted onto column CK, then we do the following
    With summary_ws
'       remove duplicate
        .Columns("CK").Resize(, 2).EntireColumn.Insert
        .Range(summary_ws.Range("CM3"), summary_ws.Range("CM3").End(xlDown)).Copy summary_ws.Range("CK3")
        .Range("CK3:CK" & summary_ws.Range("CK" & Rows.Count).End(xlUp).Row).RemoveDuplicates Columns:=1, Header:=xlNo
'       sumif
        .Range("CL3").FormulaR1C1 = "=SUMIF(C[1]:C[1],RC[-1],C[2]:C[2])"
        .Range("CL3").AutoFill Destination:=summary_ws.Range("CL3:CL" & summary_ws.Range("CK" & Rows.Count).End(xlUp).Row)
'       vlookup
        .Range("CJ3").FormulaR1C1 = _
            "=IFERROR(VLOOKUP(RC[-87],C[1]:C[2],2,FALSE),0)"
        .Range("CJ3").AutoFill Destination:=summary_ws.Range("CJ3:CJ" & last_row)
        .Range("CJ3", summary_ws.Range("CJ3").End(xlDown)).Copy
        .Range("CJ3", summary_ws.Range("CJ3").End(xlDown)).PasteSpecial xlPasteValues
'       calculate buy qty
        .Range("CG3").FormulaR1C1 = _
             "=IF(RC[-6]-RC[-8]>=0,"""",(IF(RC[-6]-RC[-7]>=0,IF(RC[-5]-RC[-8]+RC[-7]>=0,"""",IF(RC[-8]-RC[-6]-RC[-5]-RC[3]>1.5,EVEN(RC[-8]-RC[-6]-RC[-5]-RC[3]),"""")),IF(RC[-5]-RC[-8]+RC[-7]>=0,IF(RC[-7]-RC[-6]-RC[3]>1.5,EVEN(RC[-7]-RC[-6]-RC[3]),""""),IF(RC[-8]-RC[-6]-RC[-5]-RC[3]>1.5,EVEN(RC[-8]-RC[-6]-RC[-5]-RC[3]),"""")))))"
        .Range("CG3").AutoFill Destination:=summary_ws.Range("CG3:CG" & last_row)
'       lastly, clear old contents, then filter the buy qty
        .Columns("CK:CL").Delete Shift:=xlToLeft
        .Columns("CK:CL").ClearContents
        .Range("A1").Copy
        .Columns("CK:CL").PasteSpecial xlPasteFormats
         With .Range("CK2")
            .FormulaR1C1 = "Paste Purchased Parts Here"
            .Font.Color = -4165632
            .WrapText = True
            .Font.Bold = True
        End With
        .Range("A:CT").AutoFilter Field:=85, Criteria1:="<>"
    End With
'   now we copy & paste the buy qty onto the "Buy" sheet, then we are done
    Union(summary_ws.Range("B3:B" & last_row).SpecialCells(xlCellTypeVisible), summary_ws.Range("CG3:CG" & last_row).SpecialCells(xlCellTypeVisible)).Copy buy_ws.Range("A3")
    summary_ws.Range("A:CT").AutoFilter Field:=85
    
    Application.ScreenUpdating = True
    buy_ws.Activate
    Range("A3").Select
End Sub
Sub Pad_Buy()
'
'   Unlike the rotor one, we don't really need to have purchasd parts copied & pasted first,
'   just enter the qty by hand since there will not be many.
'

    Application.ScreenUpdating = False
    
'   You may want to change your worksheets names here
    Dim summary_ws, buy_ws As Worksheet
    Dim last_row As Long
    Set summary_ws = Worksheets("PAD Summary")
    Set buy_ws = Worksheets("Pad Restock")
    last_row = summary_ws.Range("A" & Rows.Count).End(xlUp).Row

'   Assume we have entered the qty into the Purchased Column, then we do the following
    With summary_ws
'       find out CDP swap qty
        .Range("S3").FormulaR1C1 = _
            "=IF(RC[19]>0,IF(RC[-2]>=RC[19],ROUND(RC[19],0),RC[-2]),0)"
        .Range("S3").AutoFill Destination:=summary_ws.Range("S3:S" & last_row)
'       calculate buy qty
        .Range("AN3").FormulaR1C1 = _
            "=IF(RC[-1]>0,IF(LEN(RC[-19])=0,IF(ROUND(RC[-1]-RC[1],0)>0,ROUND(RC[-1]-RC[1],0),""""),IF(RC[-19]+RC[1]>=RC[-1],"""",ROUND(RC[-1]-RC[-19]-RC[1],0))),"""")"
        .Range("AN3").AutoFill Destination:=summary_ws.Range("AN3:AN" & last_row)
        .Range("A:BA").AutoFilter Field:=40, Criteria1:="<>"
    End With
'   now we copy & paste the buy qty onto the "Pad Restock" sheet, then we are done
    Union(summary_ws.Range("B3:B" & last_row).SpecialCells(xlCellTypeVisible), summary_ws.Range("AN3:AN" & last_row).SpecialCells(xlCellTypeVisible)).Copy buy_ws.Range("A2")
    summary_ws.Range("A:BA").AutoFilter Field:=40
    
    Application.ScreenUpdating = True
    buy_ws.Activate
    Range("A2").Select
End Sub
